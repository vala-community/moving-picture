public class MovingPicture.Gif : Gtk.DrawingArea {
    construct {
        hexpand = true;
        vexpand = true;
    }
    
    private Gdk.Pixbuf pixbuf;
    private Gdk.PixbufAnimation anim;
    private Gdk.PixbufAnimationIter iter;

    public int width {private set; get;}
    public int height {private set; get;}

    public Gif (string location) throws Error {
        anim = new Gdk.PixbufAnimation.from_file (location);
        pixbuf = anim.get_static_image ();
        width = anim.get_width ();
        height = anim.get_height ();
        set_size_request (width, height);
        set_draw_func (drawing);
        iter = anim.get_iter(null);
        
        var idle_id = GLib.Timeout.add (10, () => {
            iter.advance (null);
            pixbuf = iter.get_pixbuf ();
            queue_draw ();
            return true;
        });

        GLib.Application.get_default ().shutdown.connect (()=> {
            Source.remove (idle_id);
        });

    }

    private void drawing (Gtk.DrawingArea drawing_area, Cairo.Context ctx, int width, int height)
    {
        Gdk.cairo_set_source_pixbuf (ctx, pixbuf, 0, 0); 
        ctx.paint();
    }
}